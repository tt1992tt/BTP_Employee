managed implementation in class zbp_i_employee_tt unique;
strict;
with draft;

define behavior for ZI_EMPLOYEE_TT alias Empleados_entity
persistent table zemployee_tt
draft table zdr_employee_tt
lock master total etag Lastchangedat // lock pesimista - global
authorization master ( instance )
//etag master LocalLastChangedAt // lock optimista - instancia
{
  field ( readonly, numbering : managed ) EmpNumber;
  field ( readonly ) icon, Category, Lastchangedat, Lastchangedby;

  create;
  update;
  delete;
  association _Activities { create; with draft; }

  draft action Edit; // son acciones implicitas pero las exige el strict
  draft action Discard;
  draft action Activate optimized;
  draft action Resume;
  draft determine action Prepare;

  action ( features : instance ) Admit result [1] $self;
  validation validateStartDate on save { create; update; }
  determination setCategory on modify { field Role; }

  mapping for zemployee_tt
  {
      EmpNumber = emp_number;
      EmpName = emp_name;
      EmpLastName = emp_last_name;
      Role = role;
      Category = category;
      Status = status;
      icon = icon;
      StartDate = start_date;
      EndDate = end_date;
      Lastchangedat = lastchangedat;
      Lastchangedby = lastchangedby;
  }
}

define behavior for ZI_ACTIVITIES_TT alias Actividades
persistent table zactivities_tt
lock dependent by _Employee
authorization dependent by _Employee
draft table zdractivities_tt
{
    update;
    delete;
    association _Employee;
    field ( readonly ) EmpNumber;
    field ( readonly:update ) ActivityId, Description;

    mapping for zactivities_tt corresponding {
    EmpNumber = emp_number;
    ActivityId = activity_id;
    Description = description;
} }